/*
 * January 2004 - Changed to support H4 device
 * Copyright (c) 2004 Texas Instruments
 *
 * (C) Copyright 2002
 * Gary Jennejohn, DENX Software Engineering, <gj@denx.de>
 *
 * (C) Copyright 2012 Freescale Semiconductor, Inc.
 *
 * See file CREDITS for list of people who contributed to this
 * project.
 *
 * This program is free software; you can redistribute it and/or
 * modify it under the terms of the GNU General Public License as
 * published by the Free Software Foundation; either version 2 of
 * the License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.	 See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 59 Temple Place, Suite 330, Boston,
 * MA 02111-1307 USA
 */

OUTPUT_FORMAT("elf32-littlearm", "elf32-littlearm", "elf32-littlearm")
OUTPUT_ARCH(arm)
ENTRY(_start)
SECTIONS
{
	. = 0x00000000;

	. = ALIGN(4);
	.text	   :
	{
	  cpu/arm_cortexa8/start.o
	  board/imx60_wario/libimx60_wario.a	(.text)
	  lib_arm/libarm.a		(.text)
	  drivers/mmc/libmmc.a		(.text)

	  *(.text)
	}

	. = ALIGN(4);
	.rodata : { *(.rodata) }

	. = ALIGN(4);
	.data : { *(.data) }

	. = ALIGN(4);
	.got : { *(.got) }

	. = .;
	__u_boot_cmd_start = .;
	.u_boot_cmd : { *(.u_boot_cmd) }
	__u_boot_cmd_end = .;

	. = ALIGN(4);
	_low_power_code_start = .;
	.low_power_code (0x900000) : 
	  AT (_low_power_code_start)
        {
	  *(.low_power_code)
	  drivers/misc/fsl_mx6sl_lpm/suspend_resume.o    (.low_power_code.suspend_resume)
	}
	_low_power_code_size = SIZEOF(.low_power_code);
	
	.low_power_data (0x900000 + _low_power_code_size) :
	  AT (_low_power_code_start + _low_power_code_size)
        {
          *(.low_power_data)
	}
	_low_power_data_size = SIZEOF(.low_power_data);
	_low_power_size = _low_power_code_size + _low_power_data_size;
	
	/* end_of ROM copy code to include Low Power code */
	_end_of_copy = __u_boot_cmd_end + _low_power_size;
	__bss_start = .;
	.bss : { *(.bss) }
	_end = .;
}
